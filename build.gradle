//file:noinspection GrMethodMayBeStatic
//file:noinspection unused
plugins {
    id "architectury-plugin" version "3.4-SNAPSHOT"
    id "dev.architectury.loom" version "1.0-SNAPSHOT" apply false
    id "io.github.pacifistmc.forgix" version "1.2.2"
}

architectury {
    minecraft = rootProject.minecraft_version
}

version = "${mod_version}+${System.getenv("GITHUB_BUILD_NUMBER") == null ? "local" : System.getenv("GITHUB_BUILD_NUMBER")}"

Object getIngame_version() {
    return "${mod_version}+${System.getenv("GITHUB_BUILD_NUMBER") == null ? "local" : System.getenv("GITHUB_BUILD_NUMBER")}"
}

forgix {
    group = maven_group
    mergedJarName = "$archives_base_name-${version}.jar"
    outputDir = "build/libs/merged"

    forge {
        projectName = "merge-forge"
        jarLocation = "build/libs/$archives_base_name-${version}.jar"
    }

    fabric {
        projectName = "merge-fabric"
        jarLocation = "build/libs/$archives_base_name-${version}.jar"
    }
}

subprojects {
    apply plugin: "dev.architectury.loom"

    loom {
        silentMojangMappingsLicense()
    }

    dependencies {
        minecraft "com.mojang:minecraft:${rootProject.minecraft_version}"
        // The following line declares the mojmap mappings, you may use other mappings as well
        mappings loom.layered() {
            officialMojangMappings()
            parchment("org.parchmentmc.data:parchment-1.19.2:2022.08.10@zip")
        }

        // The following line declares the yarn mappings you may select this one as well.
        // mappings "net.fabricmc:yarn:@YARN_MAPPINGS@:v2"
    }
}

allprojects {
    apply plugin: "java"
    apply plugin: "architectury-plugin"
    apply plugin: "maven-publish"

    archivesBaseName = rootProject.archives_base_name
    version = rootProject.version
    group = rootProject.maven_group

    repositories {
        // Add repositories to retrieve artifacts from in here.
        // You should only use this when depending on other mods because
        // Loom adds the essential maven repositories to download Minecraft and libraries from automatically.
        // See https://docs.gradle.org/current/userguide/declaring_repositories.html
        // for more information about repositories.
        maven { url = "https://maven.parchmentmc.org" }

        flatDir {
            dir "${rootProject.projectDir}/libs"
        }
    }

    tasks.withType(JavaCompile) {
        options.encoding = "UTF-8"
        options.release = 17
    }

    java {
        withSourcesJar()
    }
}

mergeJars.mustRunAfter build
mergeJars.mustRunAfter ":merge-fabric:remapJar"
mergeJars.mustRunAfter ":merge-forge:remapJar"
